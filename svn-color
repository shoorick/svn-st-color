#!/usr/bin/perl

=USAGE

    svn-color [commands and arguments]

=head1 SEE ALSO

L<http://svnbook.red-bean.com/>

=cut

use Term::ANSIColor;

use constant COLOR => {
    'blame' => {
        'new' => 'bold white',
    },
    'diff' => {
        'add'         => 'green',
        'add-file'    => 'bold green',
        'remove'      => 'red',
        'remove-file' => 'bold red',
        'lines'       => 'bold white',
        'index'       => 'bold cyan',
    },
    'help' => {
        'header' => 'bold white',
    },
    'status' => {
        'A' => 'bold green',
        'M' => 'bold yellow',
        'D' => 'bold red',
        'R' => 'bold cyan',
        'I' => 'bold black',
        'C' => 'bold white on_red',
        '!' => 'black on_red',
        '~' => 'bold magenta on_blue',
    },
};

use constant SVN => `which svn` // 'svn'; # Subversion executable

$|=1;
my    $command = join ' ', SVN =~ s/\s+$//r, @ARGV;
my $subcommand = shift;

# Show result as is if terminal doesn't support colors
print `$command`
    and exit
    if $ENV{'TERM'} !~ /color/;

foreach my $line ( `$command` ) {
    my $color;

    # non-experimental switch
    for ($subcommand) {
        if (/^(blame|ann(otate)?|praise)$/) {
            $color = COLOR->{'blame'}->{'new'}
                if $line =~ /^\s+-\s+-\s/;
        }
        elsif (/^di(ff)?/) {
            local $_ = $line;

            $color = COLOR->{'diff'}->{'add-file'}
                and next if /^\+{3}/;
            $color = COLOR->{'diff'}->{'add'}
                and next if /^\+/;
            $color = COLOR->{'diff'}->{'remove-file'}
                and next if /^-{3}/;
            $color = COLOR->{'diff'}->{'remove'}
                and next if /^-/;
            $color = COLOR->{'diff'}->{'index'}
                and next if /^(Index:|===)/;
            $color = COLOR->{'diff'}->{'lines'}
                and next if /^[@#]{2}/;
        }
        elsif (/^(h(elp)|\?)$/) {
            $color = COLOR->{'help'}->{'header'}
                if $line =~ /^(
                    Available\s+subcommands
                    |
                    Example\s+output
                    |
                    Global\s+options
                    |
                    usage
                    |
                    Valid\s+options
                ):\s/x;
        }
        elsif (/^st(at(us)?)?$/) {
            # http://svnbook.red-bean.com/nightly/en/svn.ref.svn.c.status.html
            my $char = substr $line, 0, 1;
            while ( my ( $key, $value ) = each %{ COLOR->{'status'} } ) {
                $color = $value
                #,
                #last
                    if $key eq $char;
            }
        }
    }

    print $line
    and next
        unless $color;

    chomp $line;
    print colored($line, $color), "\n";
} # foreach row in command output
